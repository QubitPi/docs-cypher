:description: Cypher has comprehensive support for lists.
include::https://raw.githubusercontent.com/neo4j-graphacademy/courses/main/asciidoc/courses/cypher-aggregation/ad.adoc[]

[[cypher-lists]]
= Lists

Cypher includes comprehensive support for lists.

[NOTE]
For information about the list predicate operator `IN`, which checks for list membership, see xref:expressions/predicates/list-operators.adoc[Expressions -> Predicates -> List operators].
For information list concatenation (`+` and `||`), list element access and slicing (`[]`), as well as list and pattern comprehensions, see xref:expressions/list-expressions.adoc[List expressions].

[[cypher-lists-general]]
== Lists in general

A literal list is created by using brackets and separating the elements in the list with commas.

.Query
[source, cypher]
----
RETURN [0, 1, 2, 3, 4, 5, 6, 7, 8, 9] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0,1,2,3,4,5,6,7,8,9]+
1+d|Rows: 1
|===

A list can consist of different value types.

.Query
[source, cypher]
----
RETURN [0, "hello", 3.14, null] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0, "hello", 3.14, null]+
1+d|Rows: 1
|===

Lists are indexed by 0 in Cypher.
To access individual elements in a list, use square brackets.
This extracts from the start index and up to, but not including, the end index.

For example:

.Query
[source, cypher]
----
WITH [5,1,7] AS list
RETURN list[2]
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list[2]+
| +7+
1+d|Rows: 1
|===

== List range and size

The below examples use the xref::functions/list.adoc#functions-range[`range`] function to create lists.
This function returns a list containing all numbers between given start and end numbers.
The range is inclusive in both ends.

.Query
[source, cypher]
----
RETURN range(0, 10)[3] AS element
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +element+
| +3+
1+d|Rows: 1
|===

It is also possible to use negative numbers, to start from the end of the list instead.

.Query
[source, cypher]
----
RETURN range(0, 10)[-3] AS element
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +element+
| +8+
1+d|Rows: 1
|===

Finally, it is possible to use ranges inside the brackets to return ranges of the list.
The list range operator (`[]`) is inclusive of the first value, but exclusive of the last value.

.Query
[source, cypher]
----
RETURN range(0, 10)[0..3] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0,1,2]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[0..-5] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0,1,2,3,4,5]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[-5..] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[6,7,8,9,10]+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[..4] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[0,1,2,3]+
1+d|Rows: 1
|===

Out-of-bound slices are simply truncated, but out-of-bound single elements return `null`.

.Query
[source, cypher]
----
RETURN range(0, 10)[15] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +<null>+
1+d|Rows: 1
|===

.Query
[source, cypher]
----
RETURN range(0, 10)[5..15] AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +[5,6,7,8,9,10]+
1+d|Rows: 1
|===

The xref::functions/scalar.adoc#functions-size[`size`] of a list can be obtained as follows:

.Query
[source, cypher]
----
RETURN size(range(0, 10)[0..3]) AS list
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +list+
| +3+
1+d|Rows: 1
|===


== Storing lists as properties

It is possible to store homogenous lists of simple values as properties.

.Allowed - store homogenous list as a property
[source, cypher]
----
CREATE (n:Label)
SET n.listProperty = [1, 2, 3]
RETURN n.listProperty AS homogenousListProperty
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| homogenousListProperty

| [1, 2, 3]

1+d|Rows: 1
|===

It is not, however, possible to store heterogeneous lists as properties.

.Not allowed - store heterogenous list as a property
[source, cypher, role=test-fail]
----
CREATE (n:Label)
SET n.listProperty = [1, "hello", .45, date()]
RETURN n.listProperty AS heterogenousListProperty
----

.Error
[source,error]
----
Neo4j only supports a subset of Cypher types for storage as singleton or array properties. Please refer to section cypher/syntax/values of the manual for more details.
----
