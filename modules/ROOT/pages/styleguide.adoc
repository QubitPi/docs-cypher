:description: The recommended style when writing Cypher queries.

[appendix]
[[cypher-styleguide]]
= Cypher styleguide


The purpose of the Cypher styleguide is to make queries as easy to read as possible.

For rules and recommendations for naming of labels, relationship types and properties, please see the xref::syntax/naming.adoc[Naming rules and recommendations].

The best way to make sure your queries follow the styling rules is to use `cypherfmt`, the official Cypher formatter.
It is available as part of the https://marketplace.visualstudio.com/items?itemName=neo4j-extensions.neo4j-for-vscode[Neo4j VS Code Extension], and as a command line tool through the https://www.npmjs.com/package/@neo4j-cypher/language-support[Neo4j Cypher Language Support npm package].

[[cypher-styleguide-general-recommendations]]
== General recommendations

* When using Cypher language constructs in prose, use a `monospaced` font and follow the styling rules.
* When referring to labels and relationship types, the colon should be included as follows: `:Label`, `:REL_TYPE`.
* When referring to functions, use lower camel case and parentheses.
For example: `toString()`.
* If you are storing Cypher statements in a separate file, use the file extension `.cypher`.

[[cypher-styleguide-indentation-and-line-breaks]]
== Indentation and line breaks

* Start a new clause on a new line.

.Bad
[source, cypher]
----
MATCH (n) WHERE n.name CONTAINS 's' RETURN n.name
----

.Good
[source, cypher]
----
MATCH (n)
WHERE n.name CONTAINS 's'
RETURN n.name
----

* Indent `ON CREATE` and `ON MATCH` with two spaces.
Put `ON CREATE` before `ON MATCH` if both are present.

.Bad
[source, cypher]
----
MERGE (n) ON CREATE SET n.prop = 0
MERGE (a:A)-[:T]->(b:B)
ON MATCH SET b.name = 'you'
ON CREATE SET a.name = 'me'
RETURN a.prop
----

.Good
[source, cypher]
----
MERGE (n)
  ON CREATE SET n.prop = 0
MERGE (a:A)-[:T]->(b:B)
  ON CREATE SET a.name = 'me'
  ON MATCH SET b.name = 'you'
RETURN a.prop
----

* Start a subquery on a new line after the opening brace, indented with two (additional) spaces.
Leave the closing brace on its own line.

.Bad
[source, cypher]
----
MATCH (a:A)
WHERE
  EXISTS { MATCH (a)-->(b:B) WHERE b.prop = 'yellow' }
RETURN a.foo
----

.Also bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS
{MATCH (a)-->(b:B)
WHERE b.prop = 'yellow'}
RETURN a.foo
----

.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {
  MATCH (a)-->(b:B)
  WHERE b.prop = 'yellow'
}
RETURN a.foo
----

* Do not break the line if the simplified subquery form is used.

.Bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {
  (a)-->(b:B)
}
RETURN a.prop
----

.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS { (a)-->(b:B) }
RETURN a.prop
----

* Limit line length to 80 characters if possible.
Line breaks should be applied starting from the outermost group, with each nested group breaking until either the line fits or no more line breaks can be inserted.
Add two spaces of indentation for each nested broken group.

.Bad
[source, cypher]
----
MATCH (n)
WHERE n.prop <> 'a' AND n.prop <> 'b' AND n.prop <> 'c' AND n.prop <> 'd' AND n.prop <> 'e'
RETURN n
----

.Good
[source, cypher]
----
MATCH (n)
WHERE
  n.prop <> 'a' AND
  n.prop <> 'b' AND
  n.prop <> 'c' AND
  n.prop <> 'd' AND
  n.prop <> 'e'
RETURN n
----

* Start `ORDER BY` and `LIMIT` clauses on new lines.

.Bad
[source, cypher]
----
MATCH (n)
RETURN 5 ORDER BY n.prop LIMIT 10
----

.Good
[source, cypher]
----
MATCH (n)
RETURN 5
ORDER BY n.prop
LIMIT 10
----

* A group containing a `CASE` expression should always break, and each `WHEN`, `ELSE`, and `END` should be put on a new line.
Additionally, `WHEN` and `ELSE` should add two spaces of indentation.

.Bad
[source, cypher]
----
MATCH (n:Person {name: 'Alice'})
RETURN CASE WHEN n.age >= 18 THEN 'Adult' ELSE 'Minor' END AS ageGroup
----

.Good
[source, cypher]
----
MATCH (n:Person {name: 'Alice'})
RETURN
  CASE
    WHEN n.age >= 18 THEN 'Adult'
    ELSE 'Minor'
  END AS ageGroup
----

* When a line ends with a list or map literal that does not fit within the maximum line width, place the opening bracket or brace on the same line as the clause that introduces it.
Start each element of the literal on a new line, indented two spaces further than the line containing the opening bracket or brace.
Close the literal at the original indentation level.

.Bad
[source, cypher]
----
RETURN 
  [
    "Alice",
    "Bob",
    "Charlie",
    "David",
    "Eve",
    "Frank",
    "Grace",
    "Heidi",
    "Ivan",
    "Judy"
  ]
----

.Good
[source, cypher]
----
RETURN [
  "Alice",
  "Bob",
  "Charlie",
  "David",
  "Eve",
  "Frank",
  "Grace",
  "Heidi",
  "Ivan",
  "Judy"
]
----

* A single blank line may be used to separate clauses, queries or comments.

.Good
[source, cypher]
----
MATCH (n)-[r]->(m)
RETURN n, r, m
----

.Also good
[source, cypher]
----
MATCH (n)-[r]->(m)

RETURN n, r, m
----

[[cypher-styleguide-casing]]
== Casing

* Write keywords in upper case.

.Bad
[source, cypher]
----
match (p:Person)
where p.name starts with 'Ma'
return p.name
----

.Good
[source, cypher]
----
MATCH (p:Person)
WHERE p.name STARTS WITH 'Ma'
RETURN p.name
----

* Write the value `null` in lower case.

.Bad
[source, cypher]
----
WITH NULL AS n1, Null AS n2
RETURN n1 IS NULL AND n2 IS NOT NULL
----

.Good
[source, cypher]
----
WITH null AS n1, null AS n2
RETURN n1 IS NULL AND n2 IS NOT NULL
----

* Write `BOOLEAN` literals (`true` and `false`) in lower case.

.Bad
[source, cypher]
----
WITH TRUE AS b1, False AS b2
RETURN b1 AND b2
----

.Good
[source, cypher]
----
WITH true AS b1, false AS b2
RETURN b1 AND b2
----

* Use camel case, starting with a lower-case character, for:
** functions
** properties
** variables
** parameters

.Bad
[source, cypher]
----
CREATE (N:Label {Prop: 0})
WITH N, RAND() AS Rand, $pArAm AS MAP
RETURN Rand, MAP.property_key, count(N)
----

.Good
[source, cypher]
----
CREATE (n:Label {prop: 0})
WITH n, rand() AS rand, $param AS map
RETURN rand, map.propertyKey, count(n)
----


[[cypher-styleguide-spacing]]
== Spacing

* For literal maps:
** No space between the opening brace and the first key
** No space between key and colon
** One space between colon and value
** No space between value and comma
** One space between comma and next key
** No space between the last value and the closing brace

.Bad
[source, cypher]
----
WITH { key1 :'value' ,key2  :  42 } AS map
RETURN map
----

.Good
[source, cypher]
----
WITH {key1: 'value', key2: 42} AS map
RETURN map
----

* One space between label/type predicates and property predicates in patterns.

.Bad
[source, cypher]
----
MATCH (p:Person{property: -1})-[:KNOWS   {since: 2016}]->()
RETURN p.name
----

.Good
[source, cypher]
----
MATCH (p:Person {property: -1})-[:KNOWS {since: 2016}]->()
RETURN p.name
----

* No space in patterns.

.Bad
[source, cypher]
----
MATCH (:Person) --> (:Vehicle)
RETURN count(*)
----

.Good
[source, cypher]
----
MATCH (:Person)-->(:Vehicle)
RETURN count(*)
----

* Use a wrapping space around operators.

.Bad
[source, cypher]
----
MATCH p=(s)-->(e)
WHERE s.name<>e.name
RETURN length(p)
----

.Good
[source, cypher]
----
MATCH p = (s)-->(e)
WHERE s.name <> e.name
RETURN length(p)
----

* No space in label predicates.

.Bad
[source, cypher]
----
MATCH (person    : Person  :  Owner  )
RETURN person.name
----

.Good
[source, cypher]
----
MATCH (person:Person:Owner)
RETURN person.name
----

* Use a space after each comma in lists and enumerations.

.Bad
[source, cypher]
----
MATCH (),()
WITH ['a','b',3.14] AS list
RETURN list,2,3,4
----

.Good
[source, cypher]
----
MATCH (), ()
WITH ['a', 'b', 3.14] AS list
RETURN list, 2, 3, 4
----

* No padding space within function call parentheses.

.Bad
[source, cypher]
----
RETURN split( 'original', 'i' )
----

.Good
[source, cypher]
----
RETURN split('original', 'i')
----

* Use padding space within simple subquery expressions.

.Bad
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS {(a)-->(b:B)}
RETURN a.prop
----

.Good
[source, cypher]
----
MATCH (a:A)
WHERE EXISTS { (a)-->(b:B) }
RETURN a.prop
----


[[cypher-styleguide-patterns]]
== Patterns

* When patterns wrap lines, break after arrows, not before.

.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)
      <--(:Country)
RETURN count(vehicle)
----

.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)<--
      (:Country)
RETURN count(vehicle)
----

* Use anonymous nodes and relationships when the variable would not be used.

.Bad
[source, cypher]
----
MATCH (kate:Person {name: 'Kate'})-[r:LIKES]-(c:Car)
RETURN c.type
----

.Good
[source, cypher]
----
MATCH (:Person {name: 'Kate'})-[:LIKES]-(c:Car)
RETURN c.type
----

* Chain patterns together to avoid repeating variables.

.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car), (vehicle:Car)-->(:Company)
RETURN count(vehicle)
----

.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)
RETURN count(vehicle)
----

* Put named nodes before anonymous nodes.

.Bad
[source, cypher]
----
MATCH ()-->(vehicle:Car)-->(manufacturer:Company)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

.Good
[source, cypher]
----
MATCH (manufacturer:Company)<--(vehicle:Car)<--()
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

* Keep anchor nodes at the beginning of the `MATCH` clause.

.Bad
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(manufacturer:Company)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

.Good
[source, cypher]
----
MATCH (manufacturer:Company)<--(vehicle:Car)<--(:Person)
WHERE manufacturer.foundedYear < 2000
RETURN vehicle.mileage
----

* Prefer outgoing (left to right) pattern relationships to incoming pattern relationships.

.Bad
[source, cypher]
----
MATCH (:Country)-->(:Company)<--(vehicle:Car)<--(:Person)
RETURN vehicle.mileage
----

.Good
[source, cypher]
----
MATCH (:Person)-->(vehicle:Car)-->(:Company)<--(:Country)
RETURN vehicle.mileage
----


[[cypher-styleguide-meta-characters]]
== Meta-characters

* Use single quotes, `'`, for literal `STRING` values.

.Bad
[source, cypher]
----
RETURN "Cypher"
----

.Good
[source, cypher]
----
RETURN 'Cypher'
----

** Disregard this rule for literal `STRING` values that contain a single quote character.
If the `STRING` has both, use the form that creates the fewest escapes.
In the case of a tie, prefer single quotes.

.Bad
[source, cypher]
----
RETURN 'Cypher\'s a nice language', "Mats' quote: \"statement\""
----

.Good
[source, cypher]
----
RETURN "Cypher's a nice language", 'Mats\' quote: "statement"'
----

* Avoid using characters and keywords that require the input to be quoted with backticks.

.Bad
[source, cypher]
----
MATCH (`odd-ch@racter$`:`Spaced Label` {`&property`: 42})
RETURN labels(`odd-ch@racter$`)
----

.Good
[source, cypher]
----
MATCH (node:NonSpacedLabel {property: 42})
RETURN labels(node)
----

* Do not use a semicolon at the end of the statement.

.Bad
[source, cypher]
----
RETURN 1;
----

.Good
[source, cypher]
----
RETURN 1
----

