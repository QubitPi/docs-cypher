= Type predicate expressions
:description: This page describes how to use type predicate expressions with Cypher.
:page-aliases: values-and-types/type-predicate.adoc

A type predicate expression can be used to verify the type of a variable, literal, property or other Cypher expression.

[[type-predicate-syntax]]
== Syntax

[source, syntax]
----
<expr> IS :: <TYPE>
----

Where `<expr>` is any Cypher expression and `<TYPE>` is a Cypher type.
For all available Cypher types, see the section on xref::values-and-types/property-structural-constructed.adoc#types-synonyms[types and their synonyms].

[[type-predicate-regular]]
== Verify the type of a Cypher expression

// tag::expressions_predicates_type_predicate[]
[source, cypher]
----
UNWIND [42, true, 'abc', NULL] AS val
RETURN val, val IS :: INTEGER AS isInteger
----
// end::expressions_predicates_type_predicate[]

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| val | isInteger

| 42 | TRUE
| TRUE | FALSE
| 'abc' | FALSE
| NULL | TRUE

2+d|Rows: 4
|===

[[type-predicate-not]]
== Type predicate expressions with NOT

It is also possible to verify that a Cypher expression is not of a certain type, using the negated type predicate expression `IS NOT ::`.

// tag::expressions_predicates_type_predicate_not[]
[source, cypher]
----
UNWIND [42, true, 'abc', NULL] AS val
RETURN val, val IS NOT :: STRING AS notString
----
// end::expressions_predicates_type_predicate_not[]

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| val | notString

| 42 | TRUE
| TRUE | TRUE
| 'abc' | FALSE
| NULL | FALSE

2+d|Rows: 4
|===


[[type-predicate-NULL]]
== Type predicate expressions for NULL

All Cypher types includes the `NULL` value.
Type predicate expressions can be appended with `NOT NULL`.
This means that `IS ::` returns `TRUE` for all expressions evaluating to `NULL`, unless `NOT NULL` is appended.

// tag::expressions_predicates_type_predicate_null[]
[source, cypher]
----
RETURN
  NULL IS :: BOOLEAN AS isBoolean,
  NULL IS :: BOOLEAN NOT NULL AS isNotNullBoolean
----
// end::expressions_predicates_type_predicate_null[]

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| isBoolean | isNotNullBoolean

| TRUE      | FALSE

2+d|Rows: 1
|===

Likewise, `IS NOT ::` returns `FALSE` for all expressions evaluating to `NULL`, unless the type is appended with `NOT NULL`.

[source, cypher]
----
RETURN
  (NULL + 1) IS NOT :: DATE AS isNotDate,
  (NULL + 1) IS NOT :: DATE NOT NULL AS isNotNotNullDate
----

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| isNotDate | isNotNotNullDate

| FALSE     | TRUE

2+d|Rows: 1
|===

It is also possible to check whether a value is the only `NULL` value using the `NULL` type.

[source, cypher]
----
RETURN NULL IS :: NULL AS isNull
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| isNull

| TRUE

1+d|Rows: 1
|===

Closed dynamic union types (`INNER_TYPE_1 | INNER_TYPE_2...`) cannot be declared as `NOT NULL`. Instead, all the inner types should be individually declared as not NULLable to achieve this behavior.

Note that all inner types in a closed dynamic union must be either NULLable, or not NULLable. This is because `NULL` values cannot be attributed to a specific type. A syntax error will be raised if the inner types are not of the same NULLability.

[source, cypher, role=test-fail]
----
RETURN 1 IS :: INTEGER NOT NULL | FLOAT
----

.Error message
[source, output]
----
All types in a Closed Dynamic Union must be NULLable, or be appended with `NOT NULL`.
----


[[type-predicate-properties]]
== Type predicate expression for properties

Type predicate expressions can also be used to filter out nodes or relationships with properties of a certain type.

A graph containing the following nodes is used for the example below:

////
[source, cypher, role=test-setup]
----
CREATE
  (:Person {name: 'Alice', age:18}),
  (:Person {name:'Bob', age:'20'}),
  (:Person {name:'Charlie', age:21})
----
////

image::type_predicate_expression_graph.svg[]

The following query finds all `Person` nodes with an `age` property that is an `INTEGER` with a value greater than `18`.

// tag::expressions_predicates_type_predicate_properties[]
[source, cypher]
----
MATCH (n:Person)
WHERE n.age IS :: INTEGER AND n.age > 18
RETURN n.name AS name, n.age AS age
----
// end::expressions_predicates_type_predicate_properties[]

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | age

| 'Charlie' | 21

2+d|Rows: 1
|===

The type `PROPERTY VALUE` can also be used to check whether a type is storable as a property.
Types not storable in properties, such as `MAP`, will return `FALSE` when checked with `IS :: PROPERTY VALUE`.

[[type-predicate-numbers]]
== Type predicate expressions for numbers of different sizes

For numerical values passed in as parameters, Cypher does not take the size of the number into account.
Cypher will therefore regard any exact numerical parameter as an `INTEGER` regardless of its declared size.
For example, an `INT16` or an `INT32` passed through from a link:https://neo4j.com/docs/create-applications/[language library] will both be treated by Cypher as an `INTEGER`.
Note that any exact numerical parameter used must fit within the range of an `INT64`.

[source, cypher, role=test-skip]
----
RETURN $int16param IS :: INTEGER AS isInteger
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| isInteger

| TRUE

1+d|Rows: 1
|===

More information about parameters can be found xref::syntax/parameters.adoc[here].

[[type-predicate-syntax-variation]]
== Syntactical variations of type predicate expressions

Type predicate expressions allow for some alternative syntax:

[source, syntax, role="noheader", indent=0]
----
<expr> IS TYPED <TYPE>
----

[source, syntax, role="noheader", indent=0]
----
<expr> :: <TYPE>
----

For verifying that an expression is not of a certain type, the following alternative syntax is supported:

[source, syntax, role="noheader", indent=0]
----
<expr> IS NOT TYPED <TYPE>
----


[[type-predicate-any-and-nothing]]
== Use of `ANY` and `NOTHING` types

`ANY` is a supertype which matches values of all types.
`NOTHING` is a type containing an empty set of values.
This means that it returns `FALSE` for all values.

[source, cypher]
----
RETURN 42 IS :: ANY AS isOfTypeAny, 42 IS :: NOTHING AS isOfTypeNothing
----

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| isOfTypeAny | isOfTypeNothing

| TRUE        | FALSE

2+d|Rows: 1
|===


[[type-predicate-closed-dynamic-unions]]
== Closed Dynamic Unions

Closed dynamic union types allow for the testing of multiple types in the same predicate.

// tag::expressions_predicates_type_predicate_closed_dynamic_union[]
[source, cypher]
----
UNWIND [42, 42.0, "42"] as val
RETURN val, val IS :: INTEGER | FLOAT AS isNumber
----
// end::expressions_predicates_type_predicate_closed_dynamic_union[]

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| val   | isNumber

| 42    | TRUE
| 42.0  | TRUE
| "42"  | FALSE

2+d|Rows: 3
|===


[[type-predicate-lists]]
== List Types

Type predicate expressions can be used for `LIST` types, where the inner type of the elements in the list must be specified.
If the inner type is not relevant, then the `ANY` type may be used.

For a `LIST` type check to return `TRUE`, all values in the list must match the inner type.

[source, cypher]
----
UNWIND [[42], [42, NULL], [42, 42.0]] as val
RETURN val, val IS :: LIST<INTEGER> AS isIntList
----

[role="queryresult",options="header,footer",cols="2*<m"]
|===
| val         | isIntList

| [42]        | TRUE
| [42, NULL]  | TRUE
| [42, 42.0]  | FALSE

2+d|Rows: 3
|===

An empty list will match on all inner types, even the `NOTHING` type.

[source, cypher]
----
RETURN
    [] IS :: LIST<NOTHING> AS isNothingList,
    [] IS :: LIST<INTEGER> AS isIntList,
    [] IS :: LIST<FLOAT NOT NULL> AS isFloatNotNullList
----

[role="queryresult",options="header,footer",cols="3*<m"]
|===
| isNothingList | isIntList | isFloatNotNullList

| TRUE          | TRUE      | TRUE

3+d|Rows: 1
|===

Lists can be combined with closed dynamic union types to create tests for heterogeneous lists.

[source, cypher]
----
WITH [1, 0, true, false] AS booleanList
RETURN booleanList IS :: LIST<BOOLEAN | INTEGER> as isMixedList
----

[role="queryresult",options="header,footer",cols="1*<m"]
|===
| isMixedList

| TRUE

1+d|Rows: 1
|===
