= Boolean operators
:description: Information about Cypher's boolean operators.
:table-caption!:

Boolean operators are used to combine or evaluate logical conditions.
Cypher contains the following boolean operators:

* Conjunction: `AND`
* Disjunction: `OR`
* Exclusive disjunction: `XOR`
* Negation: `NOT`

.Truth table for boolean operators
[options="header", cols="^,^,^,^,^,^", width="85%"]
|===
|a | b | a `AND` b | a `OR` b | a `XOR` b | `NOT` a
|`false` | `false` | `false` | `false` | `false` | `true`
|`false` | `null` | `false` | `null` | `null` | `true`
|`false` | `true` | `false` | `true` | `true` | `true`
|`true` | `false` | `false` | `true` | `true` | `false`
|`true` | `null` | `null` | `true` | `null` | `false`
|`true` | `true` | `true` | `true` | `false` | `false`
|`null` | `false` | `false` | `null` | `null` | `null`
|`null` | `null` | `null` | `null` | `null` | `null`
|`null` | `true` | `null` | `true` | `null` | `null`
|===

[[example-graph]]
== Example graph

The following graph is used for the examples below:

image::predicate_operators.svg[width="500",role="middle"]

To recreate the graph, run the following query in an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (alice:Person {name:'Alice', age: 65, role: 'Project manager', email: 'alice@company.com'}),
       (cecil:Person {name: 'Cecil', age: 25, role: 'Software developer', email: 'cecil@private.se'}),
       (cecilia:Person {name: 'Cecilia', age: 31, role: 'Software developer'}),
       (charlie:Person {name: 'Charlie', age: 61, role: 'Security engineer'}),
       (daniel:Person {name: 'Daniel', age: 39, role: 'Director', email: 'daniel@company.com'}),
       (eskil:Person {name: 'Eskil', age: 39, role: 'CEO', email: 'eskil@company.com'})
----

== Examples

.Boolean operators
=====

.`AND` operator
// tag::expressions_predicates_boolean_operators_and[]
[source, cypher]
----
MATCH (n:Person)
WHERE n.age > 30 AND n.role = 'Software developer'
RETURN n.name AS name, n.age AS age, n.role AS role
----
// end::expressions_predicates_boolean_operators_and[]

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| name | age | role

| "Cecilia" | 31 | "Software developer"

3+d|Rows: 1
|===

.`OR` operator
// tag::expressions_predicates_boolean_operators_or[]
[source, cypher]
----
MATCH (n:Person)
WHERE n.age < 30 OR n.role = 'Software developer'
RETURN n.name AS name, n.age AS age, n.role AS role
----
// end::expressions_predicates_boolean_operators_or[]

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| name | age | role

| "Cecilia" | 31  | "Software developer"
| "Cecil" | 25 | "Software developer"

3+d|Rows: 2
|===

.`XOR` operator
// tag::expressions_predicates_boolean_operators_xor[]
[source, cypher]
----
MATCH (n:Person)
WHERE n.age > 30 XOR n.role = 'Software developer'
RETURN n.name AS name, n.age AS age, n.role AS role
----
// end::expressions_predicates_boolean_operators_xor[]

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| name | age | role

| "Alice" | 65 | "Project manager"
| "Cecil" | 25 | "Software developer"
| "Charlie" | 61 | "Security engineer"
| "Eskil"   | 39  | "CEO"  
| "Daniel" | 39 | "Director"

3+d|Rows: 5
|===

.`NOT` operator
// tag::expressions_predicates_boolean_operators_not[]
[source, cypher]
----
MATCH (n:Person)
WHERE NOT n.age = 39
RETURN n.name AS name, n.age AS age
----
// end::expressions_predicates_boolean_operators_not[]

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | age

| "Alice" | 65
| "Cecil" | 25
| "Cecilia" | 31
| "Charlie" | 61

2+d|Rows: 4
|===

.Combining boolean operators
[source, cypher]
----
MATCH (n:Person)
WHERE n.role = 'Software developer' XOR (n.age > 60 AND n.role = 'Security engineer') OR NOT (n.role = 'Director' OR n.name = 'Eskil')
RETURN n.name AS name, n.age AS age, n.role AS role
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| name | age | role

| "Alice" | 65 | "Project manager"
| "Cecil"   | 25  | "Software developer" 
| "Cecilia" | 31 | "Software developer"
| "Charlie" | 61 | "Security engineer"

3+d|Rows: 4

|===

=====