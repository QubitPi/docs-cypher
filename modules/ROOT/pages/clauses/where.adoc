:description: `WHERE` adds constraints to the patterns in a `MATCH` or `OPTIONAL MATCH` clause or filters the results of a `WITH` clause.
:table-caption!:
= WHERE

The `WHERE` clause is not a clause in its own right -- rather, it is subclause used with xref:clauses/match.adoc[`MATCH`], xref:clauses/optional-match.adoc[`OPTIONAL MATCH`], and xref:clauses/with.adoc[`WITH`] clauses.

When used with `MATCH` and `OPTIONAL MATCH`, `WHERE` adds constraints to the patterns described.
_It should not be seen as a filter after the matching is finished._

In the case of multiple `MATCH` / `OPTIONAL MATCH` clauses, the predicate in `WHERE` is always a part of the patterns in the directly preceding `MATCH` / `OPTIONAL MATCH`.
Both results and performance may be impacted if `WHERE` is put inside the wrong `MATCH` clause.

When used after `WITH`, `WHERE` simply filters the results.

[TIP]
For more uses of `WHERE`, see xref:expressions/predicates/index.adoc[].

[[where-example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_where_clause.svg[width="700",role="middle"]

To recreate the graph, run the following query in an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (andy:Swedish:Person {name: 'Andy', age: 36}),
       (timothy:Person {name: 'Timothy', age: 38}),
       (peter:Person {name: 'Peter', age: 35}),
       (lisa:Person {name: 'Lisa', age: 48}),
       (john:Person {name: 'John', age: 40}),
       (susan:Person {name: 'Susan', age: 32}),
       (andy)-[:KNOWS {since: 2012}]->(timothy),
       (andy)-[:KNOWS {since: 1999}]->(peter),
       (peter)-[:KNOWS {since: 2005}]->(lisa),
       (lisa)-[:KNOWS {since: 2010}]->(john),
       (john)-[:KNOWS {since: 2021}]->(susan)
----

[[basic-filtering]]
== Basic filtering

.Filter on a node label
// tag::clauses_where_in_match_clause[]
[source, cypher]
----
MATCH (n)
WHERE n:Swedish
RETURN n.name AS name
----
// end::clauses_where_in_match_clause[]

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name

| "Andy"

1+|Rows: 1
|===

.Filter on a node property
[source, cypher]
----
MATCH (n:Person)
WHERE n.age < 35
RETURN n.name AS name, n.age AS age
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | age

| "Susan" | 32 

2+d|Rows: 1
|===

.Filter on a relationship property
// tag::clauses_where_relationship_property[]
[source, cypher]
----
MATCH (:Person {name:'Andy'})-[k:KNOWS]->(f)
WHERE k.since < 2000
RETURN f.name AS oldFriend
----
// end::clauses_where_relationship_property[]

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| oldFriend

| "Peter"

1+|Rows: 1
|===


[[filter-on-dynamic-properties]]
== Filter on dynamic properties

To filter on a property using a dynamically computed name, use square brackets `[]`:

.Parameters
[source, parameters]
----
{
  "propname": "age"
}
----

.Filter on a dynamically computed node property
[source, cypher]
----
MATCH (n:Person)
WHERE n[$propname] > 40
RETURN n.name AS name, n.age AS age
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | age

| "Lisa" | 48

2+d|Rows: 1
|===


[[where-and-with]]
== Using `WHERE` after `WITH`

xref:clauses/with.adoc[`WITH`] can be used to manipulate the output of a clause before it is passed on to subsequent query parts.
Once such a manipulation has occurred, the original clause output is not available to subsequent clauses.
For example, in the below query, `WITH` manipulates the output of the preceding `MATCH` in such a way that the succeeding `RETURN` no longer has access to the variable `n` declared in the `MATCH`.

.`WITH` only retains explicitly listed variables; others become inaccessible
[source, cypher, role=test-fail]
----
MATCH (n:Person)
WITH n.name as name
RETURN n
----

The above query would work if the `RETURN` clause instead referenced the `name` variable produced by `WITH`.
However, because `WHERE` is a subclause and not a clause, its scope is not limited by immediately preceding `WITH` clauses.

.`WHERE` is not limited by an immediately preceding `WITH`
// tag::clauses_where_with[]
[source, cypher]
----
MATCH (n:Person)
WITH n.name as name
WHERE n.age = 38
RETURN name
----
// end::clauses_where_with[]

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name

| "Timothy"

1+d|Rows: 1
|===

The name for `Timothy` is returned because the `WHERE` clause still acts as a filter on the `MATCH`.
However, `WITH` still reduces the scope for the rest of the query moving forward.
In this case, `name` is the only variable in scope for the `RETURN` clause.

[[filter-patterns]]
== Filter patterns

`WHERE` clauses can be added to xref:patterns/fixed-length-patterns.adoc[fixed-length] and xref:patterns/variable-length-patterns.adoc[variable-length patterns] in order to specify additional constraints.

[[fixed-length-patterns]]
=== Fixed-length patterns

.`WHERE` inside a node pattern
[source, cypher]
----
WITH 35 AS minAge
MATCH (a:Person WHERE a.name = 'Andy')-[:KNOWS]->(b:Person WHERE b.age > minAge)
RETURN b.name AS name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| name

| "Timothy"

1+d|Rows: 1
|===

When used this way, predicates in `WHERE` can reference the node variable that the `WHERE` clause belongs to, but not other elements of the `MATCH` pattern.

The same rule applies to pattern comprehensions:

.`WHERE` inside a pattern comprehension
// tag::clauses_where_pattern_comprehension[]
[source, cypher]
----
MATCH (a:Person {name: 'Andy'})
RETURN [(a)-->(b WHERE b:Person) | b.name] AS friends
----
// end::clauses_where_pattern_comprehension[]

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| friends
| ["Peter","Timothy"]
|Rows: 1
|===

`WHERE` can also appear inside a relationship pattern in a `MATCH` clause:

.`WHERE` inside relationship pattern
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person)-[r:KNOWS WHERE r.since < minYear]->(b:Person)
RETURN a.name AS person, b.name AS friend, r.since AS knowsSince
----

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| name | friend | knowsSince

| "Andy" | "Peter" | 1999

3+d|Rows: 1
|===

Relationship pattern predicates can also be used inside pattern comprehensions, where the same caveats apply:

.Relationship pattern predicate inside a pattern comprehension
[source, cypher]
----
WITH 2000 AS minYear
MATCH (a:Person {name: 'Andy'})
RETURN [(a)-[r:KNOWS WHERE r.since < minYear]->(b:Person) | r.since] AS years
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| years

| [1999]

1+d|Rows: 1
|===

[[variable-length-patterns]]
=== Variable-length patterns

If matching for variable length patterns, `WHERE` can only be used together with the xref:patterns/variable-length-patterns.adoc#quantified-path-patterns[quantified path pattern] or xref:patterns/variable-length-patterns.adoc#quantified-relationships[quantified relationships] syntax.

.Allowed - `WHERE` predicate inside a quantified relationship
[source, cypher]
----
MATCH p = (a:Person {name: "Andy"})-[r:KNOWS WHERE r.since < 2011]->{1,4}(:Person)
RETURN [n IN nodes(p) | n.name] AS paths
----

Note that any pathÂ´s including `Timothy` and `Susan` are excluded by the `WHERE` predicate, since their incoming `KNOWS` relationships both have a `since` value that is higher than `2011.`

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| paths

| ["Andy", "Peter"]
| ["Andy", "Peter", "Lisa"]
| ["Andy", "Peter", "Lisa", "John"]

1+d|Rows: 3
|===

For more information about using `WHERE` predicates in quantified path patterns, see xref:patterns/variable-length-patterns.adoc#quantified-path-patterns-predicates[Predicates in quantified path patterns].

`WHERE` is not allowed in variable-length patterns using the non-GQL compliant xref:patterns/reference.adoc#variable-length-relationships[variable-length relationship] syntax.

.Not allowed - `WHERE` inside a variable-length relationship
[source, cypher, role=test-fail]
----
MATCH p = (a:Person {name: 'Andy'})-[r:KNOWS*1..4 WHERE r.since < 2011]->(b:Person)
RETURN [n IN nodes(p) | n.name] AS path
----
